				'=========================================='
				
				'             The Taginator™               '
				
				'         Author: Michael Wilson           '
				
				'=========================================='
				
				'            Enterprise Edition            '
				
				'..........................................'

'This version can:
'-Create any size, display AND mobile.
'-Create tags in multiple accounts in one run.
'-Outputs a csv file that shows the Account Name, Tag ID, Tag Name, and Price Floor for easy reporting and roasting
'-Can now create tags in pending accounts, does not need approval status like in V 2.1
'-Enterprise Edition: 6 Column csv input, column 6 is file name so macro can create multiple output files in one run.

' | COL1 = Sovrn Username | COL2 = Tag Name | COL3 = Passback | COL4 = Price Floor | COL5 = Tag Size ID | COL6 = Output File Name |

'*YOU MUST BE LOGGED INTO AD CENTER*

VERSION BUILD=8961227 RECORDER=FX
SET !EXTRACT_TEST_POPUP NO
SET !ERRORIGNORE NO
SET !TIMEOUT 30
'                    |                 |
'                    |                 |
' Start of LOOP -    V INPUT CSV FILE  V   *Note: CSV File must be in Datasources Folder for access*
CMDLINE !DATASOURCE filename.csv
SET !DATASOURCE_COLUMNS 6
SET !DATASOURCE_LINE {{!LOOP}}

' Takes current browser tab to Adcenter's publisher search input box
TAB T=1
URL GOTO=adcenter.lijit.com
TAG POS=1 TYPE=SPAN ATTR=ID:navtitle_publishers
TAG POS=1 TYPE=A ATTR=ID:nav_adminpublisher_search

' Puts sovrn username in search box
TAG POS=2 TYPE=INPUT:TEXT FORM=ACTION:/adminpublisher/search ATTR=* CONTENT={{!COL1}}
TAG POS=1 TYPE=BUTTON FORM=ACTION:/adminpublisher/search ATTR=ID:PublisherSearchButton

WAIT SECONDS=3

' Pathway to specified publisher's tag creation form
TAG POS=1 TYPE=TD ATTR=TXT:*<SP>-<SP>*<SP>(*)<SP>--<SP>*
TAG POS=1 TYPE=BUTTON ATTR=ID:AffiliateEditBtn*
TAG POS=1 TYPE=A ATTR=ID:ui-menu-1-7

'This block sets the TAG FEATURE SETTINGS. At the end of each line, YES means checked, NO means unchecked.
' International Readers Ignore Floor Price
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:international CONTENT=NO
' Use EFP
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:using_efp CONTENT=YES
' Include Latitude and Longitude 
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:geo_opt_in CONTENT=NO
' Use Chain Optimization
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:chain_optimization CONTENT=NO

' Sets the tag size conditional of Tag size ID
SET !VAR0 {{!COL5}}
SET !VAR1 EVAL("var tagid='{{!VAR0}}'; if (tagid==35||tagid==25||tagid==36||tagid==43)'6'; else'0';")
TAG POS=1 TYPE=SELECT FORM=ID:ZoneForm ATTR=ID:delivery_type CONTENT=%{{!VAR1}}
TAG POS=1 TYPE=SELECT FORM=ID:ZoneForm ATTR=ID:ad_type CONTENT=%{{!COL5}}

WAIT SECONDS=1

' Conditionally marks "Mobile web" radio button depending on if tag size is mobile size
SET !ERRORIGNORE YES
SET !VAR2 EVAL("var tagtype='{{!VAR1}}'; if (tagtype=6) 'mobileWeb'; else 'null';")
TAG POS=1 TYPE=INPUT:RADIO FORM=ID:ZoneForm ATTR=ID:{{!VAR2}}
SET !ERRORIGNORE NO

' Opens Advanced Options
TAG POS=1 TYPE=A ATTR=TXT:Advanced<SP>Options

' Fills in text input fields i.e. Zone Name, Passback Code, CPM from datasource file, respectively
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:ZoneForm ATTR=ID:userzonename CONTENT={{!COL2}}
TAG POS=1 TYPE=TEXTAREA FORM=ID:ZoneForm ATTR=ID:passbacktag CONTENT={{!COL3}}
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:ZoneForm ATTR=ID:cpmfloor CONTENT={{!COL4}}

' SUBMITS THE FORM
TAG POS=1 TYPE=BUTTON FORM=ID:ZoneForm ATTR=NAME:another_button

WAIT SECONDS=1

' Creates the Output File, compiles as it runs through the loop
TAG POS=1 TYPE=LI ATTR=TXT:New<SP>Zone<SP>Created<SP>(*)New<SP>zone<SP>roasted! EXTRACT=TXT
SET !VAR3 EVAL("var s='{{!EXTRACT}}'.match(/([0-9])\\d+/); s[0];")
ADD !EXTRACT {{!VAR3}}
ADD !EXTRACT {{!COL2}}
ADD !EXTRACT {{!COL4}}
ADD !EXTRACT {{!COL1}}
SAVEAS TYPE=EXTRACT FOLDER=/Users/miwilson/desktop/AutoTags FILE={{!COL6}}
' End of Loop

				' © 2016 sovrn Holdings, Inc. '
