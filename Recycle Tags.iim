'RECYCLE TAGS

' This macro changes the name, passback, price, and size of already created tags using the tag ID#.
' Also changes the tags to EFP on, INTL off by default.

' INPUT 5 COLUMNS
'|COL1 = TAG ID# |COL2 = NEW TAG NAME|COL3 = NEW PASSBACK|COL4 = NEW CPM|COL5 = NEW TAG SIZE ID#|

'*YOU MUST BE LOGGED INTO AD CENTER*

VERSION BUILD=8961227 RECORDER=FX
SET !EXTRACT_TEST_POPUP NO
SET !ERRORIGNORE NO
SET !TIMEOUT 30
'                    |                 |
'                    |                 |
' Start of LOOP -    V INPUT CSV FILE  V   *Note: CSV File must be in Datasources Folder for access*
CMDLINE !DATASOURCE filename.csv
SET !DATASOURCE_COLUMNS 5
SET !DATASOURCE_LINE {{!LOOP}}

URL GOTO=http://adcenter.lijit.com/adminpublisher/search/zone/{{!COL1}}


'This block of code sets the TAG FEATURE SETTINGS. At the end of each line, YES means checked, NO means unchecked.
' International Readers Ignore Floor Price
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:international CONTENT=NO
' Use EFP
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:using_efp CONTENT=YES
' Include Latitude and Longitude 
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:geo_opt_in CONTENT=NO
' Use Chain Optimization
TAG POS=1 TYPE=INPUT:CHECKBOX FORM=ID:ZoneForm ATTR=ID:chain_optimization CONTENT=NO

' Sets the tag size conditional of Tag size ID
SET !VAR0 {{!COL5}}
SET !VAR1 EVAL("var tagid='{{!VAR0}}'; if (tagid==35||tagid==25||tagid==36||tagid==43)'6'; else'0';")
TAG POS=1 TYPE=SELECT FORM=ID:ZoneForm ATTR=ID:delivery_type CONTENT=%{{!VAR1}}
TAG POS=1 TYPE=SELECT FORM=ID:ZoneForm ATTR=ID:ad_type CONTENT=%{{!COL5}}

' Conditionally marks "Mobile web" radio button depending on if tag size is mobile size
SET !ERRORIGNORE YES
SET !VAR2 EVAL("var tagtype='{{!VAR1}}'; if (tagtype=6) 'mobileWeb'; else 'null';")
TAG POS=1 TYPE=INPUT:RADIO FORM=ID:ZoneForm ATTR=ID:{{!VAR2}}
SET !ERRORIGNORE NO

' Opens Advanced Options
TAG POS=1 TYPE=A ATTR=TXT:Advanced<SP>Options

' Fills in text input fields i.e. Zone Name, Passback Code, CPM from datasource file, respectively
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:ZoneForm ATTR=ID:userzonename CONTENT={{!COL2}}
TAG POS=1 TYPE=TEXTAREA FORM=ID:ZoneForm ATTR=ID:passbacktag CONTENT={{!COL3}}
TAG POS=1 TYPE=INPUT:TEXT FORM=ID:ZoneForm ATTR=ID:cpmfloor CONTENT={{!COL4}}

' SUBMITS THE FORM
TAG POS=1 TYPE=BUTTON FORM=ID:ZoneForm ATTR=ID:submit_button

WAIT SECONDS=1
ADD !EXTRACT {{!COL1}}
ADD !EXTRACT {{!COL2}}
ADD !EXTRACT {{!COL3}}
ADD !EXTRACT {{!COL4}}
' Output file is literally the same as the input, it just confirms the changes made to the tags.
SAVEAS TYPE=EXTRACT FOLDER=/Users/miwilson/desktop/AutoTags FILE=filename.csv
' End of Loop
' Author: Michael Wilson
				' © 2016 sovrn Holdings, Inc. '
  